Syntax      = ( ( EmptyLine )* Definition newline )+ ( EmptyLine )*;
Definition  = Identifier Whitespace "= " Expression ";";
Expression  = Disjunct
            | Except
			| Conjunct;
Conjunct    = Item ( " " Item )*;
Disjunct    = Atom ( newline Whitespace "| " Atom )+;
Except      = Atom ( newline Whitespace "- " Atom )+;
Item        = Repetition
            | Atom;
Repetition  = ZeroOrMore
            | ZeroOrOne
            | OneOrMore;
ZeroOrMore  = "( " Conjunct " )*";
ZeroOrOne   = "( " Conjunct " )?";
OneOrMore   = "( " Conjunct " )+";
Atom        = Identifier
            | Reserved
            | Literal;
Identifier  = ( upper ( lower )* )+;
Reserved    = ( lower )+;
Literal     = DString
            | SString;
DString     = '"' ( NoDQuote )* '"';
SString     = "'" ( NoSQuote )* "'";
NoDQuote    = print
            - '"';
NoSQuote    = print
            - "'";
Whitespace  = ( blank )+;
EmptyLine   = ( "#" ( print )* )? newline;
